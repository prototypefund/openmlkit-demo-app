plugins {
    id 'com.android.application' version '7.1.2' apply false
    id 'com.android.library' version '7.1.2' apply false
    id 'org.jetbrains.kotlin.android' version '1.6.10' apply false
    id 'maven-publish'
    id 'org.jlleitschuh.gradle.ktlint' version '10.3.0'
}

// todo this same code also appears in settings.gradle, possible to just import from there
// default token settings for interacting with gitlab maven repository when running locally
def tokenName = 'Private-Token'
def tokenValue = gitLabPrivateToken

// when running on CI, need to use different token settings
if (System.getenv("CI_JOB_TOKEN")){
    tokenName = 'Job-Token'
    tokenValue = System.getenv("CI_JOB_TOKEN")
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

publishing {
    publications {
        library(MavenPublication) {
            groupId 'io.krasch'
            artifactId 'openread'
            version System.getenv('RELEASE_VERSION')
            artifact 'openread/build/outputs/aar/openread-release.aar'

            pom.withXml {
                def dependencies = asNode().appendNode('dependencies')

                def tensorflow = dependencies.appendNode('dependency')
                tensorflow.appendNode('groupId', 'org.tensorflow')
                tensorflow.appendNode('artifactId', 'tensorflow-lite')
                tensorflow.appendNode('version', '2.8.0')

                def tensorflow_support = dependencies.appendNode('dependency')
                tensorflow_support.appendNode('groupId', 'org.tensorflow')
                tensorflow_support.appendNode('artifactId', 'tensorflow-lite-support')
                tensorflow_support.appendNode('version', '0.3.0')

                def tensorflow_ops = dependencies.appendNode('dependency')
                tensorflow_ops.appendNode('groupId', 'org.tensorflow')
                tensorflow_ops.appendNode('artifactId', 'tensorflow-lite-select-tf-ops')
                tensorflow_ops.appendNode('version', '2.8.0')

                def tensorflow_gpu = dependencies.appendNode('dependency')
                tensorflow_gpu.appendNode('groupId', 'org.tensorflow')
                tensorflow_gpu.appendNode('artifactId', 'tensorflow-lite-gpu')
                tensorflow_gpu.appendNode('version', '2.8.0')

                /*configurations.getByName("compileClasspath").getResolvedConfiguration().getFirstLevelModuleDependencies().each {
                    def dependency = dependencies.appendNode('dependency')
                    dependency.appendNode('groupId', it.moduleGroup)
                    dependency.appendNode('artifactId', it.moduleName)
                    dependency.appendNode('version', it.moduleVersion)
                }*/
            }
        }
    }
    repositories {
        maven {
            url "https://gitlab.com/api/v4/projects/38250497/packages/maven"
            credentials(HttpHeaderCredentials) {
                name = tokenName
                value = tokenValue
            }
            authentication {
                header(HttpHeaderAuthentication)
            }
        }
    }
}